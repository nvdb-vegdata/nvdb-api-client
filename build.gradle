plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
    id 'java-library'
    id 'maven-publish'
}

apply from: "gradle/git.gradle"
apply from: "gradle/versioning.gradle"
apply from: "gradle/repositories.gradle"
apply from: "gradle/publish.gradle"

group = "no.vegvesen.nvdb"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

dependencies {
    api "com.google.code.gson:gson:2.8.6"
    api "org.slf4j:slf4j-api:1.7.25"

    implementation "org.glassfish.jersey.core:jersey-client:2.32"
    implementation "org.glassfish.jersey.connectors:jersey-apache-connector:2.32"
    implementation "org.glassfish.jersey.inject:jersey-hk2:2.32"
    api "io.projectreactor:reactor-core:3.2.6.RELEASE"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.26.0"
    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.25"
}

test {
    useJUnitPlatform()
}

jar {
    manifest.attributes "Created-By": rootProject.getProperty("build.vendor")
    manifest.attributes "Implementation-Title": 'nvdb-api-client'
    manifest.attributes "Implementation-Version": project.version
}

artifacts {
    archives sourceJar
    archives javadocJar
}

task writeBuildInfo {
    doLast {
        Properties properties = new Properties();
        properties.put("git.branch", git_branch);
        properties.put("git.hash", git_shorthash);
        properties.put("git.date", git_date.toString());
        properties.put("build.version", version);
        properties.put("build.date", build_date.toString());
        properties.put("build.builder", build_builder.toString());

        def resourceDir = new File("${buildDir}/resources/main/")
        resourceDir.mkdirs()
        new File(resourceDir, "build.properties").withWriter { writer ->
            properties.store(writer, null)
        }
    }
}

processResources.dependsOn(writeBuildInfo)
